plugins {
    id "org.flywaydb.flyway" version "5.0.7"
}

group 'ibc_acamp'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply from: 'gradle/sourceSets.gradle'
apply from: 'gradle/dependencies.gradle'

applicationName = "acamp-service"
mainClassName = "com.ibc.acamp.Main"

sourceCompatibility = 1.8


repositories {
    mavenCentral()
    jcenter()
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/'
    user = 'postgres'
    password = 'mysecretpassword'
    locations = ['filesystem:src/main/resources/db/migrations']
    locations = ['filesystem:src/main/resources/db/migrations']
    schemas = ['acamp']
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.ibc.acamp.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
    archives fatJar
}

//required for heroku deploy
task stage(dependsOn: ['clean','fatJar','installDist'])

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task apiTest(type: Test) {
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
}

task testall(type: Test) {
    dependsOn test, integrationTest, apiTest
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    apiTestCompile.extendsFrom testCompile
    apiTestRuntime.extendsFrom testRuntime
    unitTestCompile.extendsFrom testCompile
    unitTestRuntime.extendsFrom testRuntime
}
